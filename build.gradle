plugins {
    id "org.springframework.boot" version "3.5.4" apply false
    id "io.spring.dependency-management" version "1.1.7" apply false
    id "java"
}

allprojects {
    group = "com.bemojr"
    version = "0.0.1-SNAPSHOT"

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: "java"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:1.18.38"
        annotationProcessor "org.projectlombok:lombok:1.18.38"
    }
}

configure(subprojects.findAll { it.name != "core" }) {
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    dependencies {
        implementation "org.springframework.boot:spring-boot-starter-web"
        implementation "org.springframework.kafka:spring-kafka"
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testImplementation "org.springframework.kafka:spring-kafka-test"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    }
}